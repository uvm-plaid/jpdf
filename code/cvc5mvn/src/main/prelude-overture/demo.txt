exprfunctions:

SHARED(x) { (m[x]@1 + m[x]@2) }

cmdfunctions:

_open(x:string, i1:cid, i2:cid){
    m[x++"exts"]@i1 := m[x++"s"]@i2;
    m[x++"extm"]@i1 := m[x++"m"]@i2;
    m[x]@i1 := (m[x++"exts"] + m[x++"s"])@i1
}

open(x:string) { _open(x,1,2); _open(x,2,1) }

_sum(z:string, x:string, y:string, i:cid){
    m[z++"s"]@i := (m[x++"s"] + m[y++"s"])@i;
    m[z++"m"]@i := (m[x++"m"] + m[y++"m"])@i;
    m[z++"k"]@i := (m[x++"k"] + m[y++"k"])@i
}

sum(z:string, x:string, y:string){ _sum(z,x,y,1); _sum(z,x,y,2) }
postcondition: ( m[z++"s"]@1 + m[z++"s"]@2 == (m[x++"s"]@1 + m[x++"s"]@2) + (m[y++"s"]@1 + m[y++"s"]@2)) )

_mult(z : string, y : string, i : cid) {
   m[z++"s"]@i := (m[z++"d"] * m[y++"s"] + -m[z++"e"] * m[z++"as"] + m[z++"cs"])@i;
   m[z++"m"]@i := (m[z++"d"] * m[y++"m"] + -m[z++"e"] * m[z++"am"] + m[z++"cm"])@i;
   m[z++"k"]@i := (m[z++"d"] * m[y++"k"] + -m[z++"e"] * m[z++"ak"] + m[z++"ck"])@i
}

precondition: ( BEAVERTRIP(z) )
mult(z : string, x : string, y : string) {
    sum(z++"d", x, z++"a");
    open(z++"d");
    sum(z++"e", y, z++"b");
    open(z++"e");
    _mult(z,y,1); _mult(z,y,2)
}
postcondition: ( m[z++"s"]@1 + m[z++"s"]@2 == (m[x++"s"]@1 + m[x++"s"]@2) * (m[y++"s"]@1 + m[y++"s"]@2) )

// *
// main entry point
// * 
precondition: ( BEAVERTRIP("k") ) 
main() {
    sum("z","x","y");
    open("z");
    mult("k","i","j");
    open("k")
}
postcondition: ( // m["z"]@1 == (m["xs"]@1 + m["xs"]@2) + (m["ys"]@1 + m["ys"]@2) AND
                 m["z"]@1 == SHARED("xs") + SHARED("ys") AND
		 m["k"]@1 == (m["is"]@1 + m["is"]@2) * (m["js"]@1 + m["js"]@2) )

constraintfunctions:

// These are Beaver triple constraints. Party 3 is a phantom just for spec.
BEAVERTRIP(x) {
  m[x++"c"]@3 == m[x++"a"]@3 * m[x++"b"]@3 AND
  m[x++"as"]@1 + m[x++"as"]@2 == m[x++"a"]@3 AND
  m[x++"bs"]@1 + m[x++"bs"]@2 == m[x++"b"]@3 AND
  m[x++"cs"]@1 + m[x++"cs"]@2 == m[x++"c"]@3 
}

