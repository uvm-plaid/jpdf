cmdfunctions:

_open(x,i1,i2){
    m[x++"exts"]@i1 := m[x++"s"]@i2;
    m[x++"extm"]@i1 := m[x++"m"]@i2;
    m[x]@i1 := (m[x++"exts"] + m[x++"s"])@i1
}

open(x) { _open(x,1,2); _open(x,2,1) }

_sum(z, x, y, i){
    m[z++"s"]@i := (m[x++"s"] + m[y++"s"])@i;
    m[z++"m"]@i := (m[x++"m"] + m[y++"m"])@i;
    m[z++"k"]@i := (m[x++"k"] + m[y++"k"])@i
}

sum(z,x,y){ _sum(z,x,y,1); _sum(z,x,y,2) }

// This clearly illustrates SHE properties of BDOZ MAC.
_mult(z, x, y, i) {
    m[z++"s"]@i := (m[z++"d"] * m[y++"s"] + -m[z++"e"] * m[z++"as"] + m[z++"cs"])@i;
    m[z++"m"]@i := (m[z++"d"] * m[y++"m"] + -m[z++"e"] * m[z++"am"] + m[z++"cm"])@i;
    m[z++"k"]@i := (m[z++"d"] * m[y++"k"] + -m[z++"e"] * m[z++"ak"] + m[z++"ck"])@i
}

mult(z,x,y) {
    sum(z++"d", x, z++"a");
    open(z++"d");
    sum(z++"e", y, z++"b");
    open(z++"e"); 
    _mult(z,x,y,1); _mult(z,x,y,2)
}

main(){
    sum("z","x","y");
    open("z");

    mult("k","i","j");
    open("k")
}

constraintfunctions:

// BDOZ MAC predicate
MACOK(x, i1, i2) { m[x++"m"]@i2 == m[x++"k"]@i1 + (m["delta"]@i1 * m[x++"s"]@i2) }

// These are Beaver triple constraints. Party 3 is a phantom just for spec.
BEAVERTRIP(x) {
  m[x++"c"]@3 == m[x++"a"]@3 * m[x++"b"]@3 AND
  m[x++"as"]@1 + m[x++"as"]@2 == m[x++"a"]@3 AND
  m[x++"bs"]@1 + m[x++"bs"]@2 == m[x++"b"]@3 AND
  m[x++"cs"]@1 + m[x++"cs"]@2 == m[x++"c"]@3
}

precondition: (
// Beaver triple constraint for mult gate k
BEAVERTRIP("k") AND

// These are BDOZ MAC constraints for party 1 verification of party 2.
MACOK("ka",1,2) AND
MACOK("kb",1,2) AND
MACOK("kc",1,2) AND
MACOK("x",1,2) AND
MACOK("y",1,2) AND
MACOK("i",1,2) AND
MACOK("j",1,2) AND

// These are BDOZ MAC constraints for party 2 verification of party 1.
MACOK("ka",2,1) AND
MACOK("kb",2,1) AND
MACOK("kc",2,1) AND
MACOK("x",2,1) AND
MACOK("y",2,1) AND
MACOK("i",2,1) AND
MACOK("j",2,1) 
)

postcondition: (
// Correctness of sum
m["z"]@1 == ((m["xs"]@1 + m["xs"]@2) + (m["ys"]@1 + m["ys"]@2)) AND

// Correctness of mult
m["k"]@1 == ((m["is"]@1 + m["is"]@2) * (m["js"]@1 + m["js"]@2)) AND

// party 1 verifications for secure openings of z and k
m["zextm"]@1 == (m["zk"]@1 + m["delta"]@1 * m["zexts"]@1) AND
m["kextm"]@1 == (m["kk"]@1 + m["delta"]@1 * m["kexts"]@1) AND

// Party 2 verifications for secure openings of z and k
m["zextm"]@1 == (m["zk"]@1 + m["delta"]@1 * m["zexts"]@1) AND
m["kextm"]@1 == (m["kk"]@1 + m["delta"]@1 * m["kexts"]@1)
)
